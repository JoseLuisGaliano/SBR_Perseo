//created on: Oct 26, 2024
package rules

//list any import classes here.
import ser.*;
import objeto.*;
import relaciones.*;
import org.kie.api.runtime.rule.QueryResults;
import org.kie.api.runtime.rule.QueryResultsRow;

//declare any global variables here

// Queries

query getInventario(Ser ser)
	$posee : Posee (poseedor == ser)
end

query getCapacidades(Ser ser)
	$capacidades : Capacidades (poseedor == ser)
end

// Reglas generales

// 1.1 - Si existe un mortal x que tiene el favor de un dios y, x obtiene los objetos de y
rule "Mortal obtiene objetos de los dioses que le favorecen"
	salience 13
	when
		$dios : Dios()
		$mortal : Mortal()
		Favor (favorecedor == $dios, favorecido == $mortal)
	then
		// Le damos los objetos al mortal
		QueryResults results = kcontext.getKieRuntime().getQueryResults("getInventario", $dios);
		for (QueryResultsRow row : results) {
			Posee p = (Posee) row.get("$posee");
			p.setPoseedor($mortal);
			update(p);			
			System.out.println($mortal.getNombre_ser() + " obtiene " + p.getObjeto().getNombre_objeto() + " debido a tener el favor de " + $dios.getNombre_ser());
		}
		// Le quitamos las capacidades a los dioses
		results = kcontext.getKieRuntime().getQueryResults("getCapacidades", $dios);
		for (QueryResultsRow row : results) {
			Capacidades c = (Capacidades) row.get("$capacidades");
			System.out.println($dios.getNombre_ser() + " pierde la capacidad " + c.getCapacidad());
			retract(c);
		}	
end

// 1.2 - Si existe un ser x que tiene un objeto especial y con propiedad z, se añade la capacidad z al ser x
rule "Ser obtiene las capacidades de sus objetos especiales"
	salience 14
	when
		$objeto : ObjetoEspecial($propiedad : tiene_propiedad)
		$ser : Ser()
		Posee (poseedor == $ser, objeto == $objeto)
		not Capacidades (poseedor == $ser, capacidad == $propiedad)
	then	
		Capacidades c = new Capacidades($ser, $propiedad);
		insert(c);
		System.out.println($ser.getNombre_ser() + " obtiene Capacidad " + $objeto.getTiene_propiedad() + " debido a tener " + $objeto.getNombre_objeto());
end

// 1.3 - Si un ser x mata a otro ser y, x obtiene los objetos de y
rule "Ser obtiene los objetos del ser al que mata"
	salience 8
	when
		$matado : Ser()
		$matador : Ser()
		Mata (matador == $matador, matado == $matado)
		Posee (poseedor == $matado) // Para evitar que se lance la regla constantemente
	then
		// Le damos los objetos al matador
		QueryResults results = kcontext.getKieRuntime().getQueryResults("getInventario", $matado);
		for (QueryResultsRow row : results) {
			Posee p = (Posee) row.get("$posee");
			p.setPoseedor($matador);
			update(p);			
			System.out.println($matador.getNombre_ser() + " obtiene " + p.getObjeto().getNombre_objeto() + " debido a matar a " + $matado.getNombre_ser());
		}
end

// 1.4 - Si un ser x mata a otro ser y, y este ser y había apresado a otro ser z, x libera a z
rule "Ser libera al apresado de a quien haya matado"
	salience 5
	when
		$apresador : Ser()
		$matador : Ser()
		$apresado : Ser()
		Mata (matador == $matador, matado == $apresador)
		$apresa : Apresa (apresador == $apresador, apresado == $apresado)
	then
		retract($apresa);
		Libera l = new Libera($matador, $apresado);
		insert(l);
		System.out.println($matador.getNombre_ser() + " libera a " + $apresado.getNombre_ser() + " debido a matar a " + $apresador.getNombre_ser());
end

// Reglas específicas

// 2.1 - Si existe un humano con nombre Casiopea y tiene el enojo de un dios con nombre Poseidon, la criatura de nombre Ceto es liberada por Poseidon
rule "Poseidon libera a Ceto"
    salience 15
    when 
        $dios : Dios (nombre_ser == "Poseidon")
        $humano : Humano (nombre_ser == "Casiopea")
        $criatura : Criatura(nombre_ser == "Ceto")
        Enojo (enojador == $humano, enojado == $dios)
        not Libera (liberado == $criatura)
    then   
        Libera l = new Libera($dios, $criatura);
        insert(l);       	
        System.out.println($criatura.getNombre_ser() + " queda libre debido a el enojo de " + $dios.getNombre_ser() + " hacia " + $humano.getNombre_ser());
end

// 2.2 - Si existe una criatura llamada Ceto y esta ha sido liberada por algún ser x, el humano de nombre Andromeda es apresado por Ceto
rule "Ceto apresa a Andromeda"   
    salience 14
    when    
        $criatura : Criatura(nombre_ser == "Ceto")
        $humano : Humano(nombre_ser == "Andromeda")
        Libera (liberado == $criatura)
        not Apresa (apresado == $humano)
        not Libera (liberado == $humano)
    then   
    	Apresa a = new Apresa($criatura, $humano);
   		insert(a);
        System.out.println($humano.getNombre_ser() + " queda presa debido a estar " + $criatura.getNombre_ser() + " libre");
end

// 2.3 - Si existe un semidios llamado Perseo que tiene el favor de un Dios llamado Hermes, el favor de un Dios llamado Atenea y la capacidad Invisibilidad, Perseo tiene el favor de el dios llamado Las Grayas
rule "Perseo obtiene favor Grayas"
	salience 11
	when	
		$dios1 : Dios (nombre_ser == "Hermes")
		$dios2 : Dios (nombre_ser == "Atenea")
		$semidios : Semidios(nombre_ser == "Perseo")
		$deidadmenor : DeidadMenor(nombre_ser == "Las Grayas")
		Favor (favorecedor == $dios1, favorecido == $semidios)
		Favor (favorecedor == $dios2, favorecido == $semidios)
		Capacidades (poseedor == $semidios, capacidad == Propiedad.INVISIBILIDAD)
		not Favor (favorecedor == $deidadmenor, favorecido == $semidios)
	then		
		Favor f = new Favor($deidadmenor, $semidios);
		insert(f);	
		System.out.println("Perseo localiza a las Grayas debido a tener el favor de Hermes y Atenea.");
		System.out.println("Perseo obtiene el Ojo/Diente de las Grayas debido tener Invisibilidad y las Grayas localizada.");
		System.out.println("Perseo obtiene el favor de las Grayas debido al intercambio de Ojo/Diente de las Grayas a las Grayas");
end

// 2.4 - Si existe un semidios llamado Perseo que tiene el favor de un Dios llamado Las Grayas, Perseo tiene el favor de el Dios llamado Ninfas
rule "Perseo obtiene favor Ninfas"
	salience 10
	when
		$dios1 : Dios (nombre_ser == "Las Grayas")
		$dios2 : Dios (nombre_ser == "Las Ninfas")
		$semidios : Semidios (nombre_ser == "Perseo")
		Favor (favorecedor == $dios1, favorecido == $semidios)
		not Favor (favorecedor == $dios2, favorecido == $semidios)
	then
		Favor f = new Favor($dios2, $semidios);
		insert(f);
		System.out.println("Perseo localiza a las Ninfas debido a tener el favor de las Grayas");
end

// 2.5 - Si existe un semidios llamado Perseo que tiene las capacidades Vuelo, Invisibilidad y Reflejo, y que tiene el objeto Hoz de Acero o el objeto Espada Indestructible, Perseo mata al Dios llamado Medusa
rule "Perseo mata a Medusa"
	salience 9
	when
		$objeto1 : Objeto (nombre_objeto == "Hoz de Acero")
		$objeto2 : Objeto (nombre_objeto == "Espada Indestructible")
		$semidios : Semidios (nombre_ser == "Perseo")
		$dios: Dios (nombre_ser == "Medusa", vivo == true)
		Capacidades (poseedor == $semidios, capacidad == Propiedad.VUELO)
		Capacidades (poseedor == $semidios, capacidad == Propiedad.INVISIBILIDAD)
		Capacidades (poseedor == $semidios, capacidad == Propiedad.REFLEJO)
		( Posee (poseedor == $semidios, objeto == $objeto1) or Posee (poseedor == $semidios, objeto == $objeto2) )
	then
		Mata m = new Mata($semidios, $dios);
		insert(m);
		modify($dios){
			setVivo(false);
		}
		System.out.println("Perseo mata a Medusa debido a tener Vuelo, Invisibilidad, Espejo y un arma");
end

// 2.6 - Si existe un semidios llamado Perseo que tiene el objeto llamado Cabeza de Medusa y no tiene el objeto Zurrón Mágico, Perseo muere
rule "Perseo muere si mata a Medusa sin el Zurron Magico"    
	salience 7
    when
    	$objeto1 : Objeto(nombre_objeto == "Cabeza de Medusa")
    	$objeto2 : Objeto(nombre_objeto == "Zurron Magico")
        $semidios : Semidios(nombre_ser == "Perseo", vivo == true) 
        Posee (poseedor == $semidios, objeto == $objeto1)
        not Posee (poseedor == $semidios, objeto == $objeto2)
    then
        modify($semidios) {
            setVivo(false);
        }        
        System.out.println("Perseo muere al coger la cabeza de Medusa sin tener el Zurron Magico");
end

// 2.7 - Si existe un semidios llamado Perseo que tiene el objeto llamado Cabeza de Medusa y existe una criatura llamada Ceto que haya apresado a un humano llamado Andromeda, Perseo mata a Ceto
rule "Perseo mata a Ceto"
	salience 6
	when
		$objeto : Objeto (nombre_objeto == "Cabeza de Medusa")
		$semidios : Semidios (nombre_ser == "Perseo", vivo == true)
		$criatura : Criatura (nombre_ser == "Ceto", vivo == true)
		$humano : Humano (nombre_ser == "Andromeda");
		Posee (poseedor == $semidios, objeto == $objeto)
		Apresa (apresador == $criatura, apresado == $humano)
	then
		Mata m = new Mata($semidios, $criatura);
		insert(m);
		modify($criatura) {
			setVivo(false);
		}
		System.out.println("Perseo mata Ceto debido a tener Cabeza de Medusa.");
end

// 2.8 - Si existe un semidios llamado Perseo que tiene el objeto llamado Bridas de Oro, Perseo obtiene el objeto llamado Pegaso
rule "Perseo obtiene a Pegaso"
	salience 12
	when
		$objeto1 : Objeto (nombre_objeto == "Bridas de Oro")
		$objeto2 : Objeto (nombre_objeto == "Pegaso")
		$semidios : Semidios (nombre_ser == "Perseo")
		Posee (poseedor == $semidios, objeto == $objeto1)
		not Posee (poseedor == $semidios, objeto == $objeto2)
	then
		Posee p = new Posee($semidios, $objeto2);
		insert(p);
		System.out.println("Perseo obtiene a Pegaso debido a tener las Bridas de Oro.");
end